---
title: 'Using SASS and Compass with Gradle'
date: 2013-04-26T13:58:00+0100
tags: gradle, sass
alias: ["post/48926142330/using-sass-and-compass-with-gradle/"]
---

I recently started helping with the https://github.com/ratpack[Ratpack] website. It is (or will be) a Ratpack app & built with Gradle. I started prototyping with a simple webapp created with http://yeoman.io/[Yeoman] and using http://sass-lang.com/[SASS] and http://compass-style.org/[Compass] for authoring CSS. When I migrated the work-in-progress into the https://github.com/ratpack/ratpack-site[_ratpack-site_ application] I initially used http://naleid.com/blog/2013/01/24/calling-gruntjs-tasks-from-gradle/[Ted Naleid's method] of calling Yeoman's http://gruntjs.com/[Grunt] tasks from Gradle. Unfortunately this meant there were rather a lot of build dependencies. In order to build the app you would need Node.js, Ruby and the Compass gem installed. Peter Ledbrook pointed out this could frustrate potential contributors & Marcin Erdmann https://twitter.com/marcinerdmann/status/325675229454155776[proved an example] of what he meant. Clearly I needed to simplify.

SASS, particularly with Compass, is great for CSS authoring. Mixins & functions like http://compass-style.org/reference/compass/utilities/color/contrast/[`contrasted`] or http://compass-style.org/reference/compass/helpers/image-dimensions/[`image-height`] and the http://compass-style.org/reference/compass/typography/vertical_rhythm/[vertical rhythm] support have become invaluable to me. However, they are Ruby gems with no ports to other languages.

I wanted to at least try to get it working before giving up and using LESS. I found a few posts where people had done something similar but mostly they were either half-complete, using older Gradle syntax or exhibiting the same dependency problems I already had. They did give me the idea to use JRuby to run SASS, though.

I came across several solutions that advised either packaging desired gems in a jar file or actually re-packing the JRuby jar with additional gems installed. Neither of these appealed to me. I didn't want to publish new binary artifacts or untangle any potential licensing issues. It seemed to me that I should be able to use JRuby to install the gems I needed somewhere local to the build and then refer to them.

[[installing-ruby-gems]]
Installing Ruby gems
~~~~~~~~~~~~~~~~~~~~

First I created a Gradle task called _installGems_ to install the _compass_ gem. This uses the Gradle _JavaExec_ task to run `gem install` using JRuby and specifies the path to install the gems as `.jruby/gems`. That directory is also the task's _output_ so that further tasks can depend on it as part of the incremental build.

JRuby itself is added to a new dependency scope since I'm only using it for running build tasks, not as part of the application itself.

[source,groovy]
--------------------------------------------------------------------------
configurations {
    jruby
}

dependencies {
    jruby 'org.jruby:jruby-complete:1.7.3'
}

ext {
    gemsDir = file('.jruby/gems')
}

task installGems(type: JavaExec) {
    outputs.dir gemsDir

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S gem install -i $gemsDir --no-rdoc --no-ri compass".tokenize()

    doFirst {
        gemsDir.mkdirs()
    }
}
--------------------------------------------------------------------------

Initially I installed the gems under `build` but that would mean any time `gradle clean` is run they will be deleted. Since they are not particularly fast to install and shouldn't change I moved them elsewhere.

[[compiling-sass]]
Compiling SASS
~~~~~~~~~~~~~~

Next I created a _compileSass_ task. The trick is to supply two environment variables; _GEM_PATH_ which tells JRuby where to look for installed gems and _PATH_ pointing to the `.jruby/gems/bin` directory where the _compass_ executable is installed.

The _compileSass_ task's _inputs_ are the _outputs_ of _installGems_ – meaning _installGems_ is run automatically if necessary – along with the directories containing my _.scss_ files, images and JavaScript. The task's _output_ is the directory with the compiled CSS.

[source,groovy]
-----------------------------------------------------------------------------------------------------------------------------------------------
ext {
    cssDir = file(/* output dir where CSS should go */)
    sassDir = file(/* location of .sass / .scss files */)
    imagesDir = file(/* location of images */)
    javascriptsDir = file(/* location of javascripts */)
}

task compileSass(type: JavaExec) {
    outputs.dir cssDir
    inputs.files installGems
    inputs.dir sassDir
    inputs.dir imagesDir
    inputs.dir javascriptsDir

    classpath = configurations.jruby
    main = "org.jruby.Main"
    args "-S compass compile --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets"
    environment 'GEM_PATH', gemsDir
    environment 'PATH', "$gemsDir/bin"

    doFirst {
        cssDir.mkdirs()
    }
}
-----------------------------------------------------------------------------------------------------------------------------------------------

I also added _compileSass_ into the task dependency chain so it would automatically run when needed:

[source,groovy]
-----------------------------------------
processResources.inputs.files compileSass
clean.dependsOn cleanCompileSass
-----------------------------------------

I then created my own _JRubyExec_ task type that extends _JavaExec_ to bundle together some of the commonalities between _installGems_ and _compileSass_.

[[watching-for-changes]]
Watching for changes
~~~~~~~~~~~~~~~~~~~~

The next step was to use the `compass watch` command to automatically recompile SASS changes while the application is running. The _ratpack-site_ app uses the http://www.gradle.org/docs/current/userguide/application_plugin.html[Gradle application plugin] to provide a _run_ task. I needed to create a background thread that runs `compass watch`. I couldn't figure out how to re-use my _JRubyExec_ task here as I needed to use the imperative style of calling `project.javaexec` so I've ended up with some duplication. It does work, though, which is the main thing.

[source,groovy]
-------------------------------------------------------------------------------------------------------------------------------------------------------------------------
task watchSass {
    doFirst {
        cssDir.mkdirs()

        Thread.start {
            project.javaexec {
                classpath = configurations.jruby
                main = 'org.jruby.Main'
                args "-X-C -S compass watch --sass-dir $sassDir --css-dir $cssDir --images-dir $imagesDir --javascripts-dir $javascriptsDir --relative-assets".tokenize()
                environment 'GEM_PATH', gemsDir
                environment 'PATH', "$gemsDir/bin"
            }
        }
    }
}
run.dependsOn watchSass
-------------------------------------------------------------------------------------------------------------------------------------------------------------------------

[[performance-tuning]]
Performance tuning
~~~~~~~~~~~~~~~~~~

Once everything was working I spent a little time performance tuning. The JRuby wiki has https://github.com/jruby/jruby/wiki/PerformanceTuning#java-virtual-machine-jvm-settings[some tips about JVM flags] useful for JRuby execution & I added `-client -XX:+TieredCompilation -XX:TieredStopAtLevel=1` which improved things a little. Following http://blog.headius.com/2010/03/jruby-startup-time-tips.html[a tip found on Charles Nutter's blog] I also added `-X-C` to the JRuby command itself which disables the JRuby JIT compiler which also seemed to help a little.

Compiling SASS this way is still slower than using native Ruby – either standalone or via Grunt – but it's not painfully slow and the tradeoff in terms of build simplicity is worth it.

[[next-steps]]
Next steps
~~~~~~~~~~

This is not a perfect or finished solution. It contains some duplication, a mixture of declarative and imperative task styles, no proper _sourceSet_ for SASS. When time permits I'd like to get this bundled up as a proper Gradle plugin or possibly two – one for generic JRuby execution and another specifically for SASS.

I should also point out that Marcin Erdmann and Luke Daley were a big help in getting this soluton working. My Gradle-fu is shaky at best and they helped me a lot with declaring the incremental build properly and getting the background thread for the _watchSass_ task working.
