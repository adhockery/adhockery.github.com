---
title: 'Modelling repeating structures with Geb page objects'
date: 2010-11-26T00:17:00+0000
tags: geb, testing
alias: ["post/42903103830/modelling-repeating-structures-with-geb-page-objects/"]
---

http://geb.codehaus.org/[Geb] is the hot new thing in Grails functional testing. One of its most powerful features is the concise DSL for defining page objects. The reasons for using page objects are http://code.google.com/p/selenium/wiki/PageObjects[well] http://robfletcher.github.com/grails-selenium-rc/docs/manual/guide/4.%20Using%20Page%20Objects.html[enumerated] http://geb.codehaus.org/manual/latest/pages.html#the_page_object_pattern_why[elsewhere] but the basic point is to allow your tests to interact with pages in a manner agnostic of the detail of their structure. This is both practical (you can change markup structure without having to fix numerous tests that only fail because they were tightly coupled to that structure) and aesthetic (your tests read more like a user's interaction with the page - the _what_ rather than the _how_).

I want to put together a few short blog posts dealing with patterns that I find useful for defining page objects and modules in Geb. As I go I'll keep adding to a very simple Grails project showing working examples which is https://github.com/robfletcher/geb-examples/[available on GitHub].

In this post I want to talk about repeating data structures such as lists and tables and how to model them effectively.

A _content_ property in a Geb `Page` or `Module` can be any type; whatever is returned from the defining closure. This will frequently be a Geb `Navigator` instance or a `String` but can be whatever is useful for the tests you're writing. A good rule of thumb is that the test should be dealing with as simplified a view of the data as possible. All the complexity of traversing HTML elements and manipulating them into a useful form should be hidden away in the page objects and modules. When handling repeating data structures such as `ol` or `table` elements you probably want to be able to treat the content as a `List` so that tests can use Groovy features such as iterator methods, indexing and slicing to make very expressive assertions.

[[simple-repeating-structures]]
Simple repeating structures
~~~~~~~~~~~~~~~~~~~~~~~~~~~

For example, imagine we want to verify an `ol` element like this:

[source,markup]
---------------------------------
<ol id="recent-books">
    <li>Zero History</li>
    <li>Surface Detail</li>
    <li>The Machine of Death</li>
</ol>
---------------------------------

The most useful type would probably be a `List<String>` which we can get easily enough by defining our content like this:

[source,groovy]
---------------------------------------------------
static content = {
    recentBooks { $("ol#recent-books li")*.text() }
}
---------------------------------------------------

This is then very easy to use in a test:

[source,groovy]
-------------------------------------------------------------------------
expect:
recentBooks == ["Zero History", "Surface Detail", "The Machine of Death"]
-------------------------------------------------------------------------

[[complex-repeating-structures-using-modules]]
Complex repeating structures using Modules
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

A more complex example of a repeating structure is a table, where each row contains several fields. Here we can use a Geb `Module` to represent each row, with content properties to get data from each cell. Let's say we want to verify the contents of the following table of search results:

[source,markup]
----------------------------------------
<table id="book-results">
    <thead>
        <tr>
            <th>Title</th>
            <th>Author</th>
            <th>Format</th>
            <th>Price</th>
            <th>Release Date</th>
        </tr>
    </thead>
    <tbody>
        <tr>
            <td>Zero History</td>
            <td>William Gibson</td>
            <td>Hardback</td>
            <td>£12.29</td>
            <td>2 Sep 2010</td>
        </tr>
        <tr>
            <td>Zero History</td>
            <td>William Gibson</td>
            <td>Kindle</td>
            <td>£11.99</td>
            <td>2 Sep 2010</td>
        </tr>
        <tr>
            <td>Spook Country</td>
            <td>William Gibson</td>
            <td>Paperback</td>
            <td>£5.00</td>
            <td>31 Jul 2008</td>
        </tr>
        <tr>
            <td>Pattern Recognition</td>
            <td>William Gibson</td>
            <td>Paperback</td>
            <td>£4.99</td>
            <td>24 Jun 2004</td>
        </tr>
    </tbody>
</table>
----------------------------------------

We can define our module like this:

[source,groovy]
----------------------------------------------------------------------------
static content = {
    bookResults { i -> module BookRow, $("table#book-results tbody tr", i) }
}

class BookRow extends Module {
    static content = {
        cell { i -> $("td", i) }
        title { cell(0).text() }
        author { cell(1).text() }
        format { cell(2).text() }
        price { cell(3).text()[1..-1].toDouble() }
        releaseDate { cell(4).text() }
    }
}
----------------------------------------------------------------------------

The `bookResults` content closure takes a row number parameter and uses it to select the corresponding `tr` from the body of the table and use that to construct a module. The module itself defines content properties with meaningful names that map to the text in each cell.

This isn't bad as far as it goes. We can use the module pretty effectively in tests like this:

[source,groovy]
--------------------------------------
expect:
bookResults(0).title == "Zero History"
bookResults(3).price == 4.99
--------------------------------------

However, `bookResults` isn't a `List`. We can't easily get all the book titles at once or make an assertion that all the authors are the same or find the lowest price. Even querying how many rows there are would require an additional content property or method to retrieve `$("tbody tr").size()`. The table is a repeating data structure and it would be nice to treat it as one!

This ought to be possible bearing in mind 3 things:

1.  The type of a content property is simply whatever you return from the defining closure, there's no reason we can't return a `List<BookRow>`.
2.  There's nothing special about the expression that constructs the module itself: `module BookRow, $("tbody tr", i)` is just a call to a method called `module` passing a `Class<? extends Module>` which is the module type we want and a `Navigator` pointing to the module's root element.
3.  The Geb `Navigator` class returned by the `$` expression implements `Iterable<Navigator>` and can be treated like a `List` of all the selected HTML elements.

In fact we can get a `List<BookRow>` easily enough if we redefine the `bookResults` property like this:

[source,groovy]
-------------------------------
static content = {
    bookResults {
        $("tbody tr").collect {
            module BookRow, it
        }
    }
}
-------------------------------

The key here is that we iterate over the `tr` elements inside the content definition collecting a new `BookRow` instance for each one. Now the page object doesn't require the test to pass in the index of the row it's interested in. This enables our test to do some much more powerful and interesting things:

[source,groovy]
--------------------------------------------------------------------------------------
expect:
bookResults.size() == 4
bookResults[0].title == "Zero History"
bookResults.title.unique() == ["Zero History", "Spook Country", "Pattern Recognition"]
bookResults.every { it.author == "William Gibson" }
bookResults[2..3].every { it.format == "Paperback" }
bookResults.price.sum() == 34.27
--------------------------------------------------------------------------------------

I've tried to show a couple of reasonably simple examples here. Others are easy to imagine; a `Map` representing the `dt` and `dd` elements inside an HTML definition list, a list of modules representing a group of labelled radio buttons or news items with images and links, a tree-like multi-level navigation structure, etc.
